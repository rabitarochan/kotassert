buildscript {
    ext.kotassert_version = "0.1.0"
    ext.kotlin_version = "1.0.4"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


// project settings

apply plugin: "kotlin"
apply plugin: "jacoco"
apply plugin: "maven"
apply plugin: "signing"

group = "com.github.rabitarochan"
version = kotassert_version

sourceCompatibility = 1.6
targetCompatibility = 1.6
tasks.withType(SourceTask).findAll { it.hasProperty("options") } *.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // junit, hamcrest
    compile "junit:junit:4.12"
    compile "org.hamcrest:hamcrest-all:1.3"
}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        testLogging.showStandardStreams = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


// maven publish settings

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name "kotassert"
                packaging "jar"
                description "JUnit support library for Kotlin."
                url "https://github.com/rabitarochan/kotassert"

                scm {
                    connection "scm:git:https://github.com/rabitarochan/kotassert"
                    developerConnection "scm:git:https://github.com/rabitarochan/kotassert"
                    url "https://github.com/rabitarochan/kotassert"
                }

                licenses {
                    license {
                        name "The MIT License (MIT)"
                        url "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id "rabitarochan"
                        name "Kengo Asamizu"
                        email "rabitarochan@gmail.com"
                    }
                }
            }
        }
    }
}


// wrapper

task wrapper(type: Wrapper) {
    gradleVersion = "3.1"
}
